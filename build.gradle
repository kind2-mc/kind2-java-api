plugins {
  id 'com.palantir.git-version' version '0.12.3'
  id 'java-library'
  id 'maven-publish'
  id 'org.jreleaser' version '1.20.0'
}

group = 'edu.uiowa.cs.clc'
version = gitVersion()
ext.isReleaseVersion = version.matches("[0-9]+.[0-9]+.[0-9]+")

repositories {
  mavenCentral()
}

dependencies {
  implementation 'com.google.code.gson:gson:2.8.6'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
  useJUnitPlatform()
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      pom {
        name = 'Kind 2 Java API'
        description = 'An API for constructing Lustre programs, running Kind 2, and showing results/suggestions.'
        url = 'https://github.com/kind2-mc/kind2-java-api'
        licenses {
          license {
            name = 'BSD 3-Clause License'
            url = 'https://github.com/kind2-mc/kind2-java-api/blob/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'daniel-larraz'
            name = 'Daniel Larraz'
            email = 'daniel-larraz@uiowa.edu'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/kind2-mc/kind2-java-api.git'
          developerConnection = 'scm:git:ssh:github.com/kind2-mc/kind2-java-api.git'
          url = 'https://github.com/kind2-mc/kind2-java-api'
        }
      }
    }
  }

  repositories {
    maven {
        url = layout.buildDirectory.dir('staging-deploy')
    }
  }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
            github {
                packages {
                    active = 'ALWAYS'
                    url = 'https://maven.pkg.github.com/kind2-mc/kind2-java-api'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

